apply plugin: 'com.android.application'
apply plugin: 'com.mxalbert.gradle.jacoco-android'

jacoco {
    toolVersion = "0.8.10"
}

android {
    namespace 'ai.elimu.kukariri'
    compileSdk 35

    defaultConfig {
        applicationId "ai.elimu.kukariri"
        minSdkVersion 24
        targetSdkVersion 35
        versionCode 1000005
        versionName "1.0.5-SNAPSHOT"
        setProperty("archivesBaseName", "${applicationId}-${versionCode}")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            manifestPlaceholders = [contentProviderApplicationId: "ai.elimu.content_provider.debug", analyticsApplicationId: "ai.elimu.analytics.debug"]
            buildConfigField("String", "CONTENT_PROVIDER_APPLICATION_ID", '"ai.elimu.content_provider.debug"')
            buildConfigField("String", "ANALYTICS_APPLICATION_ID", '"ai.elimu.analytics.debug"')
        }
        qa_test {
            initWith debug
            applicationIdSuffix ".test"
            versionNameSuffix "-test"
            manifestPlaceholders = [contentProviderApplicationId: "ai.elimu.content_provider.test", analyticsApplicationId: "ai.elimu.analytics.test"]
            buildConfigField("String", "CONTENT_PROVIDER_APPLICATION_ID", '"ai.elimu.content_provider.test"')
            buildConfigField("String", "ANALYTICS_APPLICATION_ID", '"ai.elimu.analytics.test"')
        }
        release {
            minifyEnabled false
            manifestPlaceholders = [contentProviderApplicationId: "ai.elimu.content_provider", analyticsApplicationId: "ai.elimu.analytics"]
            buildConfigField("String", "CONTENT_PROVIDER_APPLICATION_ID", '"ai.elimu.content_provider"')
            buildConfigField("String", "ANALYTICS_APPLICATION_ID", '"ai.elimu.analytics"')
        }
    }

    buildFeatures {
        buildConfig = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.github.elimu-ai:model:model-2.0.83' // See https://jitpack.io/#elimu-ai/model
    implementation 'com.github.elimu-ai:content-provider:1.2.28@aar' // See https://jitpack.io/#elimu-ai/content-provider
    implementation 'com.github.elimu-ai:analytics:3.1.27@aar' // See https://jitpack.io/#elimu-ai/analytics

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'

    implementation ('com.sackcentury:shinebutton:1.0.0') {
        exclude group: 'com.daasuu', module: 'EasingInterpolator'
    }
    implementation 'com.github.MasayukiSuda:EasingInterpolator:v1.3.2'

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
